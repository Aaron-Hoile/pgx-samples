/*
 * Copyright (C) 2013 - 2019 Oracle and/or its affiliates. All rights reserved.
 */
procedure personalized_pagerank_from_kind(G: graph, filter: string,
 specialty: nodeProp<string>, e,d: double, max_iter_count: int;
  pg_rank: nodeProp<double>)
{
  if (G.numNodes() == 0){
    return;
  }

  nodeProp<bool> is_start;
  G.is_start = _.specialty == filter;
  int M = count(n : G.nodes)(n.specialty == filter);
  double diff;
  int cnt = 0;

  G.pg_rank = (_.is_start) ? 1.0/M : 0;

  do {
    diff = 0.0;
    foreach (t: G.nodes) {
      double val1 = (t.is_start) ? (1-d) : 0;
      double val2 = d * sum(w: t.inNbrs) {w.pg_rank / w.outDegree()} ;
      double val = val1 + val2;
      diff += | val - t.pg_rank |;
      t.pg_rank <= val;
    }
    cnt++;
  } while ((diff > e) && (cnt < max_iter_count));
}